/** 
    SINGLY LINKED LIST 1 - USING NULL-TERMINATED RECURSIVE PREDICATES
*/

field elem: Int
field next: Ref

/** singly linked list that cannot be empty */
predicate list(this: Ref) {
    acc(this.elem) && acc(this.next) && (this.next != null ==> list(this.next)) 
}

/** singly linked list that can be empty */
predicate list2(this: Ref) {
    this != null ==> acc(this.elem) && acc(this.next) && list(this.next) 
}

/** An iterative implementation of reversing a singly linked list 
    using the list predicate. Returns a reference to the new head of the list */
method reverseIter(head: Ref) returns (res: Ref)
    requires list(head)
    // requires head != null
    ensures list(res)
    ensures old(length(head)) == length(res)
    
    ensures forall i : Int :: i >= 0 && i < length(res) ==> 
        get(res, length(res) - i - 1) == old(get(head, i))
{
    var prev : Ref := null
    var curr : Ref := head
    var len : Int := length(head)
    
    unfold list(head)   // required for the verifier to assert that head != null if it is not given in the pre-condition
    fold list(head)

    while (curr != null)
        invariant curr != null ==> list(curr)
        invariant curr == null ==> prev != null
        invariant prev != null ==> list(prev)
        invariant length(prev) + length(curr) == len
        invariant forall i : Int :: i >= length(prev) && i < len ==> get(curr, i - length(prev)) == old(get(head, i))
        invariant forall j : Int :: j >= 0 && j < length(prev) ==> old(get(head, j)) == get(prev, length(prev) - 1 - j)
    {
        unfold acc(list(curr))

        var nextTmp : Ref := curr.next
        curr.next := prev
        
        fold list(curr)
        
        prev := curr
        curr := nextTmp
    }
    
    res := prev
}

/** An iterative implementation of reversing a singly linked list 
    using the list2 predicate. Returns a reference to the new head of the list */
method reverseIter2(head: Ref) returns (res: Ref)
    requires list(head)
    ensures list(res)
    ensures old(length(head)) == length(res)
    ensures forall i : Int :: i >= 0 && i < length(res) ==> 
        get(res, length(res) - i - 1) == old(get(head, i))
{
    if (head == null) {
        res := head
    } else { 
        res := reverseIter(head)
    }
}

method get_reference2(head: Ref, index: Int) returns (res: Ref)
    requires head != null ==> list(head)
    requires index >= 0 && index < length(head)
    ensures list(res)
    ensures list(res) --* list(head) && elems(head) == old(elems(head)[..index]) ++ old[lhs](elems(res))
{
    if (index == 0) {
        res := head
        package list(res) --* list(head) && elems(head) == old(elems(head)[..index]) ++ old[lhs](elems(res))
    } else {
        unfold list(head)
        var prev : Ref := head
        var curr : Ref := head.next
        var i : Int := 1

        package list(curr) --* list(head) && elems(head) == old(elems(head)[..i]) ++ old[lhs](elems(curr))
        {
            fold list(head)
        }

        while (i != index)
            invariant curr != null ==> list(curr)
            invariant i >= 1 && i <= index
            invariant old(length(head)) - i == length(curr)
            invariant curr != null ==> elems(curr) == old(elems(head))[i..]
            invariant list(curr) --* list(head) && elems(head) == old(elems(head)[..i]) ++ old[lhs](elems(curr))
        {   
            unfold list(curr)
            prev := curr
            curr := curr.next
            i := i + 1

            package list(curr) --* list(head) && elems(head) == old(elems(head)[..i]) ++ old[lhs](elems(curr))
            {
                fold list(prev)
                apply list(prev) --* list(head) && elems(head) == old(elems(head)[..i-1]) ++ old[lhs](elems(prev))
            }
        }
        res := curr
    }
}

method refactoredInsert2(head: Ref, index: Int, val: Int) returns (res: Ref)
    requires list(head)
    requires index >= 0 && index <= length(head)
    ensures list(res)
    // ensures old(length(head)) - index == length(res)
    // ensures old(elems(head)[..index-1]) == elems(res)[..index-1]
{
    var tmp : Ref 
    tmp := new(*)
    tmp.elem := val

    if (index == 0) {
        tmp.next := head
        res := tmp
        fold list(res)
    } else {
        var curr : Ref
        curr := get_reference2(head, index - 1)

        unfold list(curr)
        var lenRest : Int := length(curr.next)
        var nextNode : Ref := curr.next
        
        curr.next := tmp
        tmp.next := nextNode
        res := head

        fold list(tmp)
        fold list(curr)

        assert getElem(curr, 1) == val
        
        apply list(curr) --* list(head) && elems(head) == old(elems(head)[..index-1]) ++ old[lhs](elems(curr))
        // assert old(elems(head))[..1] == elems(res)[..1]
        assume index < length(res)
        // assert get(res, index) == tmp
        // assert getElem(res, index) == val
        // ++ Seq(val) ++ elems(res)[index + 1..]
    }
}   

method get_reference(head: Ref, index: Int) returns (res: Ref)
    requires head != null ==> list(head)
    requires index >= 0 && index < length(head)
    ensures list(res)
    ensures list(res) --* list(head) 
        && length(head) - index == old[lhs](length(res))
        && forall j : Int :: j >= index && j < length(head) ==> get(head, j) == old[lhs](get(res, j - index))
        && forall k : Int :: k >= 0 && k < index ==> get(head, k) == old(get(head, k))
    
{
    if (index == 0) {
        res := head
        package list(res) --* list(head)
            && length(head) - index == old[lhs](length(res))
            && forall j : Int :: j >= index && j < length(head) ==> get(head, j) == old[lhs](get(res, j - index))
            && forall k : Int :: k >= 0 && k < index ==> get(head, k) == old(get(head, k))
    } else {
        var prev : Ref := null
        var curr : Ref := head
        var i : Int := 0

        package list(curr) --* list(head) 
            && length(head) - i == old[lhs](length(curr))
            && forall j : Int :: j >= i && j < length(head) ==> get(head, j) == old[lhs](get(curr, j - i))
            && forall k : Int :: k >= 0 && k < i ==> get(head, k) == old(get(head, k))

        while (i != index)
            invariant curr != null ==> list(curr)
            invariant i >= 0 && i <= index
            invariant old(length(head)) - i == length(curr)
            invariant forall j : Int :: j >= i && j < old(length(head)) ==> old(get(head, j)) == get(curr, j - i)
            invariant list(curr) --* list(head)
                && length(head) - i == old[lhs](length(curr))
                && forall j : Int :: j >= i && j < length(head) ==> get(head, j) == old[lhs](get(curr, j - i))
                && forall k : Int :: k >= 0 && k < i ==> get(head, k) == old(get(head, k))
        {   
            unfold list(curr)
            prev := curr
            curr := curr.next
            i := i + 1

            package list(curr) --* list(head) 
                && length(head) - i == old[lhs](length(curr))
                && forall j : Int :: j >= i && j < length(head) ==> get(head, j) == old[lhs](get(curr, j - i))
                && forall k : Int :: k >= 0 && k < i ==> get(head, k) == old(get(head, k))
            {
                fold list(prev)
                assert i >= 0 && i <= index
                apply list(prev) --* list(head)
                    && length(head) - (i-1) == old[lhs](length(prev))
                    && forall j : Int :: j >= (i-1) && j < length(head) ==> get(head, j) == old[lhs](get(prev, j - (i-1)))
                    && forall k : Int :: k >= 0 && k < (i-1) ==> get(head, k) == old(get(head, k))
            }
        }
        res := curr
    }
}

method refactoredInsert(head: Ref, index: Int, val: Int) returns (res: Ref)
    requires list(head)
    requires index >= 0 && index <= length(head)
    ensures list(res)
    // ensures old(length(head)) + 1 == length(res)
    // ensures forall i : Int :: i > index && i < old(length(head)) ==> old(get(head, i)) == get(res, i - index)
    // ensures getElem(res, index) == val
    // ensures forall i : Int :: i > index && i < length(res) ==> old(get(head, i + (index - 1))) == get(res, i)
    // ensures old(elems(head)[..index]) == elems(res)[..index]
{
    var tmp : Ref 
    tmp := new(*)
    tmp.elem := val

    if (index == 0) {
        tmp.next := head
        res := tmp
        fold list(res)
    } else {
        var curr : Ref
        curr := get_reference(head, index - 1)

        unfold list(curr)
        var lenRest : Int := length(curr.next)
        var nextNode : Ref := curr.next
        
        curr.next := tmp
        tmp.next := nextNode
        res := head

        fold list(tmp)
        fold list(curr)
        
        apply list(curr) --* list(head) 
            && length(head) - (index-1) == old[lhs](length(curr))
            && forall j : Int :: j >= (index-1) && j < length(head) ==> get(head, j) == old[lhs](get(curr, j - (index-1)))
            && forall k : Int :: k >= 0 && k < (index-1) ==> get(head, k) == old(get(head, k))
        // assert forall j : Int :: j >= index && j < old(length(head)) ==> old(get(head, j)) == get(res, j + 1)
        // assert forall j : Int :: j >= index + 1 && j < old(length(head)) ==> old(get(head, j)) == get(res, j + 1)
    }
}

/* inserts a new node before the node at index and returns the head of the list */
method insert(head: Ref, index: Int, val: Int) returns (res: Ref)
    requires list(head)
    requires index >= 0 && index <= length(head)
    ensures list(res)
{
    var tmp : Ref 
    tmp := new(*)
    tmp.elem := val
    
    if (head == null || index == 0) {
        tmp.next := head
        res := tmp
        fold list(res)
    } else {
        var i : Int := index
        var len : Int := length(head)
        
        unfold list(head)
        
        var prev : Ref := null
        var curr : Ref := head
        var nextNode : Ref := head.next

        package acc(curr.next) && acc(curr.elem) && list(curr.next) --* list(head) {
            fold list(curr)
        }

        while (i > 1) 
            invariant acc(curr.next) && acc(curr.elem)
            invariant nextNode != null ==> list(nextNode)
            invariant i - 2 < length(nextNode)
            invariant curr.next == nextNode
            invariant length(nextNode) + 1  == i + old(length(head)) - index
            invariant acc(curr.next) && acc(curr.elem) && list(curr.next) --* list(head)
        {
            prev := curr
            curr := nextNode

            if (nextNode != null) {
                unfold list(nextNode)
                nextNode := nextNode.next
            }
            i := i - 1
            package acc(curr.next) && acc(curr.elem) && list(curr.next) --* list(head)
            {
                fold list(curr)
                apply acc(prev.next) && acc(prev.elem) && list(prev.next) --* list(head) 
            }
        }

        curr.next := tmp
        tmp.next := nextNode
        res := head
        fold list(tmp)
        
        apply acc(curr.next) && acc(curr.elem) && list(curr.next) --* list(head)
    }
}

function length(head: Ref) : Int
    requires head != null ==> list(head)
    ensures head == null ==> result == 0
    ensures head != null ==> result > 0
{
    head == null ? 0 : unfolding list(head) in length(head.next) + 1
}

function segLength(head: Ref, end: Ref) : Int
    requires head != end ==> list(head)
    ensures head == end ==> result == 0
    ensures head != end ==> result > 0
{
    head == end ? 0 : unfolding list(head) in length(head.next) + 1
}

function get(head: Ref, i: Int) : Ref
    requires acc(list(head))
    requires i >= 0 && i < length(head)
{
    i == 0 ? head : unfolding acc(list(head)) in get(head.next, i - 1)
}

function getElem(head: Ref, i: Int) : Int
    requires acc(list(head))
    requires i >= 0 && i < length(head)
{
    unfolding acc(list(head)) in
        i == 0 ? head.elem : getElem(head.next, i - 1)
}

function content(head: Ref) : Seq[Int]
    requires head != null ==> acc(list(head))
    ensures length(head) == |result|
{
    head == null ? Seq[Int]() : 
        unfolding list(head) in Seq(head.elem) ++ content(head.next)   
}

function elems(head: Ref) : Seq[Int]
  requires list(head)
{
  unfolding list(head) in (
    (head.next == null ? Seq(head.elem) :
     Seq(head.elem) ++ elems(head.next) ))  
}
